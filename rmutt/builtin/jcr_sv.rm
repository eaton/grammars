#include "xml.rm"
#include "eng.rm"
#include "sentence.rm"
#include "url.rm"

package jcr_sv;

top:
  (i="")
  xml.header "\n"
  rootNode;

rootNode:
  (attrs=
    xml.attr["xmlns:ns",url.url] " "
    xml.attr["xmlns:jcr","http://www.jcp.org/jcr/1.0"] " "
    xml.attr["xmlns:nt","http://www.jcp.org/jcr/nt/1.0"] " "
    xml.attr["xmlns:sv","http://www.jcp.org/jcr/sv/1.0"])
  node["jcr:root",attrs];

node[n,attrs]:
  (attrs=attrs" "xml.attr["sv:name",n])
  i xml.start["sv:node",attrs] "\n"
  ((i=i"  ")
   primaryType
   properties
   children)
  i xml.end["sv:node"] "\n";

primaryType:
  property["jcr:primaryType","Name",value["nt:base"]];

properties:
  property[name,"String",value[sentence.s]{1,3}] |
  property[name,"Long",value[long]{1,3}] |
  property[name,"Binary",value[binary]] |
  property[name,"Double",value[double]] |
  property[name,"Name",value[name]] |
  property[name,"Boolean",value[boolean]] |
  property[name,"Path",value[path]] |
  property[name,"Date",value[date]]
;

property[name,type,values]:
  i xml.start["sv:property",xml.attr["sv:name",name]" "xml.attr["sv:type",type]] "\n"
    values
  i xml.end["sv:property"] "\n";

value[content]:
  i "  " xml.start["sv:value",] content xml.end["sv:value"] "\n";

long:
  "-"? chars.nzDigit chars.digit{0,5};

binary:
  ((chars.alpha 52 | chars.digit 10 | "/" | "+"){4}){0,256};

double:
  "-"? chars.nzDigit chars.digit{0,5} "." chars.digit{1,5};

boolean:
  "true" | "false";

path:
  (".." ("/.."){0,3})?
  ("/" name ("[" chars.nzDigit "]")?){0,4};

date:
  year "-" month "-" day "T" hour ":" minute ":" second "." fracSeconds tzd;

year:
  ("19"|"20") chars.digit{2};

month:
  "01","02","03","04","05","06","07","08","09","10","11","12";

day:
  "01","02","03","04","05","06","07","08","09","10","11","12","13","14","15",
  "16","17","18","19","20","21","22","23","24","25","26","27","28","29";

hour:
  "00","01","02","03","04","05","06","07","08","09","10","11","12",
  "13","14","15","16","17","18","19","20","21","22","23";

minute:
  ("0","1","2","3","4","5") chars.digit;
second: minute;

fracSeconds:
  chars.digit{5};

tzd: "-05:00";

children:
  child{0} 16 |
  child{1} 8 |
  child{2} 4 |
  child{3} 2 |
  child{4} 1;

child: node[name,];

name:
  "ns:" eng.noun_singular > /[^A-Za-z]/_/;